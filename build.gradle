plugins {
    id 'fabric-loom' version '0.9.+'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

def minecraft_version = "1.17.1"
def parchment_version = "2021.08.22"
def loader_version = "0.11.6"
def fabric_version = "0.39.2+1.17"

archivesBaseName = "HealthOverlay-${minecraft_version}"
version = "4.0.2"
group = "terrails.healthoverlay"

minecraft { }

repositories {
    maven {
        name 'ParchmentMC'
        url 'https://maven.parchmentmc.org/'
    }
    maven {
        name 'TerraFormersMC'
        url 'https://maven.terraformersmc.com/'
        content {
            includeGroup 'com.terraformersmc'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // ModMenu
    modRuntime "com.terraformersmc:modmenu:2.0.6"

    // Fiber Config
    modApi "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
    it.options.release = 16
}

java {
    withSourcesJar()
}

jar {
    from ("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}