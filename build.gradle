plugins {
    id 'fabric-loom' version '0.9.+'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def minecraft_version = "1.16.5"
def parchment_version = "2021.08.08"
def loader_version = "0.11.6"
def fabric_version = "0.39.2+1.16"

archivesBaseName = "HealthOverlay-${minecraft_version}"
version = "4.0.0"
group = "terrails.healthoverlay"

minecraft { }

repositories {
    maven {
		name 'ParchmentMC'
		url 'https://maven.parchmentmc.org/'
	}
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name 'TerraFormersMC'
        url 'https://maven.terraformersmc.com/'
        content {
            includeGroup 'com.terraformersmc'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // ModMenu
    modRuntime "com.terraformersmc:modmenu:1.16.11"

    // Stats Keeper for testing
    modRuntime "curse.maven:statskeeper-318348:3406374" // 1.16.5-3.2.8

    // Fiber Config
    modApi "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}

jar {
    from ("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}